# ==================================================
# vars

config("libuv_config") {
  include_dirs = [
    "include",
  ]
  if (is_win) {
    libs = [
      "advapi32.lib",
      "iphlpapi.lib",
      "psapi.lib",
      "shell32.lib",
      "user32.lib",
      "userenv.lib",
      "ws2_32.lib",
    ]
  } else {
    libs = [
      "pthread",
    ]
    if (!is_mac) {
      libs += [
        "dl",
        "rt",
      ]
    }
  }
}

static_library("libuv") {
  sources = [
    "include/uv.h",
    "include/tree.h",
    "include/uv-errno.h",
    "include/uv-threadpool.h",
    "include/uv-version.h",
    "src/fs-poll.c",
    "src/heap-inl.h",
    "src/idna.c",
    "src/idna.h",
    "src/inet.c",
    "src/queue.h",
    "src/random.c",
    "src/strscpy.c",
    "src/strscpy.h",
    "src/threadpool.c",
    "src/timer.c",
    "src/uv-common.c",
    "src/uv-common.h",
    "src/uv-data-getter-setters.c",
    "src/version.c",
  ]
  include_dirs = [
    "src",
  ]
  if (is_win) {
    defines = [
      "WIN32_LEAN_AND_MEAN",
      "_WIN32_WINNT=0x0600",
      "_GNU_SOURCE",
    ]
    sources += [
      "include/uv-win.h",
      "src/win/async.c",
      "src/win/atomicops-inl.h",
      "src/win/core.c",
      "src/win/detect-wakeup.c",
      "src/win/dl.c",
      "src/win/error.c",
      "src/win/fs-event.c",
      "src/win/fs-fd-hash-inl.h",
      "src/win/fs.c",
      "src/win/getaddrinfo.c",
      "src/win/getnameinfo.c",
      "src/win/handle-inl.h",
      "src/win/handle.c",
      "src/win/internal.h",
      "src/win/loop-watcher.c",
      "src/win/pipe.c",
      "src/win/poll.c",
      "src/win/process-stdio.c",
      "src/win/process.c",
      "src/win/req-inl.h",
      "src/win/signal.c",
      "src/win/snprintf.c",
      "src/win/stream-inl.h",
      "src/win/stream.c",
      "src/win/tcp.c",
      "src/win/thread.c",
      "src/win/tty.c",
      "src/win/timer.c",
      "src/win/udp.c",
      "src/win/util.c",
      "src/win/winapi.c",
      "src/win/winapi.h",
      "src/win/winsock.c",
      "src/win/winsock.h",
    ]
  } else {
    defines = [
      "_LARGEFILE_SOURCE",
      "_FILE_OFFSET_BITS=64",
    ]
    sources += [
      "include/uv-unix.h",
      "include/uv-linux.h",
      "include/uv-sunos.h",
      "include/uv-darwin.h",
      "include/uv-bsd.h",
      "include/uv-aix.h",
      "src/unix/async.c",
      "src/unix/atomic-ops.h",
      "src/unix/core.c",
      "src/unix/dl.c",
      "src/unix/fs.c",
      #"src/unix/fsevents.c",
      "src/unix/getaddrinfo.c",
      "src/unix/getnameinfo.c",
      "src/unix/internal.h",
      "src/unix/loop-watcher.c",
      "src/unix/loop.c",
      "src/unix/pipe.c",
      "src/unix/poll.c",
      #"src/unix/posix-hrtime.c",
      #"src/unix/posix-poll.c",
      "src/unix/process.c",
      #"src/unix/procfs-exepath.c",
      "src/unix/proctitle.c",
      #"src/unix/pthread-fixes.c",
      "src/unix/random-devurandom.c",
      #"src/unix/random-getentropy.c",
      #"src/unix/random-getrandom.c",
      "src/unix/signal.c",
      "src/unix/spinlock.h",
      "src/unix/stream.c",
      #"src/unix/sysinfo-loadavg.c",
      #"src/unix/sysinfo-memory.c",
      "src/unix/tcp.c",
      "src/unix/thread.c",
      "src/unix/tty.c",
      "src/unix/udp.c",
    ]
    if (is_mac) {
      defines += [
        "_DARWIN_UNLIMITED_SELECT=1",
        "_DARWIN_USE_64_BIT_INODE=1",
      ]
      sources += [
        "src/unix/darwin-proctitle.c",
        "src/unix/darwin-stub.h",
        "src/unix/darwin.c",
        "src/unix/bsd-ifaddrs.c",
        "src/unix/kqueue.c",
        "src/unix/random-getentropy.c",
        "src/unix/fsevents.c",
      ]
    } else {
      defines += [
        "_GNU_SOURCE",
        "_POSIX_C_SOURCE=200112L",
      ]
      sources += [
        "src/unix/linux-core.c",
        "src/unix/linux-inotify.c",
        "src/unix/linux-syscalls.c",
        "src/unix/linux-syscalls.h",
        "src/unix/procfs-exepath.c",
        "src/unix/random-getrandom.c",
        "src/unix/random-sysctl-linux.c",
      ]
    }
    cflags = [
      "--std=gnu89",
      "-pedantic",
      "-fvisibility=hidden",
      "-Wstrict-aliasing",
      #"-fPIC",
    ]
  }
  if (is_win) {
    all_dependent_configs = [ ":libuv_config" ]
  } else {
    public_configs = [ ":libuv_config" ]
  }
}

#executable("sample_uv") {
#  sources = [ "sample_uv.c" ]
#  deps = [ ":libuv" ]
#}

